---
title: 어셈블리어 기초 2) 기본 명령어
categories: OS
tags: 
toc: true
toc_sticky: true
---

## **1. 사칙 연산**

**1) add, 더하기 연산**
```
add a, b
```
$a$에 $a+b$ 값을 넣어주는 연산. 

이때, 조건이 몇가지 있다.

- a는 레지스터 혹은 메모리여야 한다. 

- b는 레지스터 혹은 메모리 혹은 상수여야 한다.

- 단, a, b 모두 메모리면 안된다. 

- b에는 a보다 큰 크기(byte)의 값이 올 수 없다.

<br/>

메모리에 상수를 더하는 예시는 아래와 같다. mov로 메모리에 상수를 넣었을 때와 마찬가지로 상수의 크기를 표시해주어야 한다.

```
section .text
global CMAIN

CMAIN:
    add [a], byte 1 
    ...

section .bss
    a resb 1
    
```

**2) sub, 빼기 연산**
```
sub a, b
```
$a$에 $a-b$ 값을 넣어주는 연산. 사용하는 방식 및 유의사항은 add와 동일하다. 

<br/>

**+) 왜 add memory, memory 연산은 안될까?**

[참고1](https://www.quora.com/Why-is-it-not-possible-to-add-the-contents-of-two-memory-locations-variables-in-8086-assembly-language),  [참고2](https://myfreechild.tistory.com/entry/Assembly-%EC%97%B0%EC%82%B0%EC%97%90%EC%84%9C-Memory-To-Memory-%EC%97%B0%EC%82%B0%EC%9D%B4-%EB%B6%88%EA%B0%80%EB%8A%A5%ED%95%9C-%EC%9D%B4%EC%9C%A0)

비효율적이기 때문에 설계 과정에서 배제되었을 것이라는 추측이 주도적이다. 한 명령어에서 두 메모리 접근을 가능하게 하려면 처리해야 되는 비용이 늘어난다는 것, 그리고 그렇게 했을 때 큰 이점이 없다는 것이 이유이다. 

이런 추측에 대해 자세히 이해하려면 명령어 사이클에 대해 이해해야 한다. [명령어 사이클](https://chw-owo.github.io/computer/structure/%EB%AA%85%EB%A0%B9%EC%96%B4-%EC%82%AC%EC%9D%B4%ED%81%B4/)

명령어 사이클을 보면 레지스터에 있는 값을 더하거나, 상수 값을 더하는 것과 달리 메모리에 있는 값을 가져오기 위해서는 MAR, MBR을 거치는 다소 복잡한 과정이 필요함을 알 수 있다. 두 메모리에 있는 값을 가져와 더하는 과정을 임의로 디자인해보면 아래와 같다.

```

//1. 첫번째 오퍼랜드 인출
MAR <- PC
MBR <- M[MAR], PC <- PC + 1
IR     <- MBR

// 2. 임시 저장
MAR <- IR
MBR <- M[MAR]
AC    <- MBR

//3. 두번째 오퍼랜드 인출
MAR <- PC
MBR <- M[MAR], PC <- PC + 1
IR     <- MBR

//4. 대상 메모리 주소를 엑세스하여 저장
MAR <- IR
M[MAR] <- AC

```
보다시피 이렇게 되면, 한 사이클에 1 마이크로 연산이라는 가정 하에 한 명령어가 최소 11사이클을 차지하는 셈이 된다. Program Control Unit의 Control Memory에서 위와 같이 큰 루틴을 저장하게 되면 기억 장소의 낭비가 클 것이며, 동일한 과정을 거치는 2번의 인출 과정을 하나의 루틴으로 구성하기 때문에 비효율적일 것이다. 또, 기존의 일반적인 프로세서의 Data Bus는 8bit - 64bit 사이의 크기를 가지는데, 한 명령어에서 두 가지 operand 모두에 대해 주소 지정이 가능하려면 32bit 프로세서 기준 최소 64bit가 주소 지정에 쓰여야 한다. 한 명령어에서 두 메모리에 동시에 접근하기 위해서는 Data Bus의 수용 크기 뿐 아니라 MBR, IR, IBR 등 레지스터의 수용 크기 역시 커져야하는데 이럴 경우 프로세서 제조 비용이 상승하게 된다. 

참고로 일부 CISC 아키텍쳐에서는 한 명령어에서 두 가지 operand 모두 메모리를 쓸 수 있도록 설계한 경우도 있다고 한다. RISC의 경우 한 사이클에 하나의 Instruction 처리를 설계의 기본으로 하기 때문에 메모리 접근, Instruction 인출, 읽기/쓰기 등을 한 사이클 내에 처리할 수 없어서 이러한 설계가 더 어려운 반면 CISC는 하나의 Instruction이 여러개의 Micro-Operation 조합으로 구성될 수 있기 때문에, 비용만 감수한다면 설계가 가능하다고 한다. 

**3) mul, 곱하기 연산**

곱하기 나누기 연산에는 몇 가지 규칙이 있다. 
```
mul bl
```
위와 같이 입력할 경우 al * bl의 연산 결과가 ax에 저장된다.

```
mul bx
```
위와 같이 입력할 경우 ax * bx의 연산 결과 중 상위 16bits는 dx에, 하위 16bits는 ax에 저장하게 된다.

따라서 만약 5 * 8을 ax에 저장하고 싶다면 아래와 같이 쓸 수 있다.

```
mov ax, 0
mov al, 5
mov bl, 8
mul bl
```

**4) div, 나누기 연산**

부호 없는 정수의 나눗셈을 수행하며, 뒤에 레지스터와 메모리만 올 수 있고 상수는 올 수 없다.

```
div bl
```
위와 같이 입력할 경우 ax/bl 의 연산 결과 중 몫을 al에, 나머지를 ah에 넣게 된다. 

따라서 만약 100 / 3의 몫과 나머지를 각각 al, ah에 저장하고 싶다면 아래와 같이 쓸 수 있다.

```
mov ax, 100
mov bl, 3
div bl
```

## **2. 시프트 연산**

**1) shr, shl**

shift, 영어 단어 뜻 그대로 옮기는 연산이다. 

shift left의 준말인 shl의 경우 왼쪽으로 밀고 그 자리에 0을 채우는 것을 의미한다.

```
mov eax, 0x12345678
shl eax, 8
```
eax를 8bit 만큼 왼쪽으로 옮기고(shift) 그 자리에 0을 채워넣는 것이므로 연산 후의 eax 값은 0x34567800이 된다. 

반대로 오른쪽으로 다시 당길 수도 있다. 

shift right의 준말인 shr의 경우 오른쪽으로 밀고 그 자리에 0을 채우는 것을 의미한다.

```
shr eax, 8
```

이렇게 할 경우, eax는 처음 값인 0x12345678으로 돌아오는 게 아니라 0x00345678이라는 값이 된다. 기존에 어떤 값이었든 간에 옮겼던 만큼 0으로 채우기 때문이다. 

**2) shift 연산의 응용**

2진수에서는 왼쪽으로 갈 수록 각 bit가 * 2 만큼 더 큰 숫자를 의미하므로 왼쪽으로 한칸을 미는 행동은 * 2 를, 오른쪽으로 한칸을 미는 행동은 / 2 를 하는 것과 같은 결과가 나온다. 즉, 2의 배수에 해당하는 곱하기 나누기 연산을 보다 빠르게 수행할 수 있게 된다. 

계산기에서 시행할 경우 산술적 시프트와 논리적 시프트가 나누어져 있기도 하다. 보통 2진수의 맨 앞자리는 부호 비트로 사용되는데, 산술적 시프트의 경우에는 부호 비트는 유지한 채 나머지 부분에서 shift가 일어나고, 논리적 시프트의 경우에는 맨 앞자리도 포함해서 shift가 일어난다. 

그 외에도 게임 제작 시 ObjectID를 만들 때 활용하거나 상태를 정의할 때 활용하는 등 메모리 공간을 줄이거나, 성능을 향상하기 위해 응용하기도 한다. 

<br/>

## **3. 논리 연산**

**1) AND 연산자**

연산 대상의 각 bit를 비교하여 모두 1인 경우에만 앞에 있는 피연산자의 해당 bit가 1이 된다.

즉, 연산 대상의 각 bit를 비교하여 둘 중 하나라도 0이면 0이 된다.


```
mov al, 0b10010101
mov bl, 0b01111100
and al, bl
```

이 경우 al은 0b00010100이 된다

<br/>

**2) OR 연산자**

연산 대상의 각 bit를 비교하여 하나라도 1인 경우 결과가 1이 된다.

즉, 연산 대상의 각 bit를 비교하여 모두 0이면 0이 된다.

```
mov al, 0b10010101
mov bl, 0b01111100
or al, bl
```

이 경우 al은 0b11111101이 된다

<br/>


**3) XOR 연산자**

EXclusive OR, 배타적 OR이라는 뜻으로 

연산 대상의 각 bit를 비교하여 값이 서로 다른 경우에만 1이 된다.

즉, 연산 대상의 각 bit를 비교하여 서로 값이 같은 경우 0이 된다. 

```
mov al, 0b10010101
mov bl, 0b01111100
xor al, bl
```

이 경우 al은 0b11101001이 된다

동일한 값으로 xor을 할 경우 값은 무조건 0이 된다. 이에 동일한 값으로 xor을 또 할 경우 자기 자신으로 되돌아오는 특성을 가진다. 논리 연산은 기본적으로 속도가 빠르기 때문에 xor을 사용할 경우 0을 mov로 직접 대입하는 것보다 더 빠르게 값을 초기화할 수 있다. 

참고로 SASM 환경에서 설정을 변경하지 않고 새 어셈블리어 프로젝트를 만들면 다음과 같은 초기 화면이 뜬다. 이때 보면 return 직전에 xor가 들어가있는 것을 확인할 수 있다. 

![image](https://user-images.githubusercontent.com/96677719/210223670-56d71665-4f5e-45c8-b791-e7fa0a3b9b14.png)

accumulator register인 ax, 즉 산술 연산 시 값을 저장하는 데 사용되었던 레지스터를 효율적으로 초기화해주는 과정이다. 어셈블리어에서는 함수를 종료할 때 ax에 들어있던 값을 반환하는데 이때 0을 반환함으로써 프로그램이 무사히 종료되었음을 전달하게 된다.

<br/>

**4) NOT 연산자**

연산 대상의 각 bit가 1일 경우 0을, 0일 경우 1을 반환한다.

즉, 값을 반대로 반전시켜준다.

```
mov al, 0b10010101
not al
```

이 경우 al은 0b01101010이 된다

<br/>

**5) 진리표**

![image](https://user-images.githubusercontent.com/96677719/149709008-187f03c0-3635-4cb7-8ca7-f595a42e818d.png)

<br/>

**6) bit flag**

각 bit에 용도를 부여하여 적은 용량으로 상태를 표현하는 것에 사용할 수 있다. 

<br/>

## **4. 분기문과 반복문**

조건을 확인하는 명령어는 다음과 같다.

```
cmp dst, src
```

dst, src의 값을 비교하여 비교의 결과물은 flag register에 저장한다. (어셈블러 레지스터 창에서 eflag를 확인하면 된다) 그리고 그 값을 활용해 이후에 나오는 명령어에 따라 분기를 탄다.

```
jmp  [label]   ; jump to label.
je   [label]   ; jump to label if equal. (dst == src)
jne  [label]   ; jump to label if not equal. (dst != src)
jg   [label]   ; jump to label if greater. (dst > src)
jge  [label]   ; jump to label if greater or equal .(dst >= src)
jl   [label]   ; jump to label if less. (dst < src)
jle  [label]   ; jump to label if less or equal. (dst <= src)
```
간단하게 ax에 있는 숫자가 짝수면 1, 홀수면 0을 출력하는 프로그램을 만든다고 가정해보면 다음과 같이 만들 수 있다.

```
CMAIN:

    mov ax, --

    mov bl, 2
    div bl          ; ax/bl 후 나머지를 ah에 저장한다
    cmp ah, 0       ; 나머지가 0이면, 즉 짝수라면
    je LABEL_ODD    ; LABEL_ODD 위치로 이동한다

    mov rcx, 0      ; LABEL_ODD로 이동하지 않았다면 (홀수라면) rcx에 0을 넣는다.
    jmp LABEL_PRINT

LABEL_ODD:
    mov rcx, 1      

LABEL_PRINT:
    PRINT_HEX 1, rcx ; 결과물을 출력한다.

    xor rax, rax
    ret 

```
Label은 이동할 위치를 표시해주는 것이지 함수가 아니다. 따라서 이동한 이후에는 다시 위로 돌아가지 않고, 순차적으로 아랫줄로 내려간다.

<br/>

분기문을 이용해서 반복문을 만들 수도 있다.

```
CMAIN:

    mov ecx, 10

LABEL_LOOP:
    PRINT_STRING msg
    NEWLINE
    dec ecx     ; = sub ecx, 1
    cmp ecx, 0
    jne LABEL_LOOP

    xor rax, rax
    ret

section .data
    msg db 'Hello World', 0x00

```

시행할 때 마다 ecx = ecx - 1을 시행하여 ecx == 0이 되기 전까지 라벨 위치로 돌아간다. 

<br/>

## **5. 배열**

**배열, array**     : 동일한 타입의 데이터 묶음

**요소, element**   : 배열을 구성하는 각 값

**인덱스, index**   : 배열 위치를 가리키는 숫자

어셈블리어에서 배열을 선언하는 것에는 아래와 같은 방법들이 있다.

```
section .data
    msg db 'Hello World', 0x00          
    ; 1 byte * 11(글자수) = 11 byte 의 배열(문자열)

    a db 0x01, 0x02, 0x03, 0x04, 0x05   
    ; 5 * 1 = 5 byte의 배열

    b times 5 dw 1                      
    ; 5 * 2 = 10 byte의 배열

section .bss
    num resb 10

```

<br/>

Little-Endian을 사용하는 환경(대부분의 Intel 데스크톱)에서 위와 같이 배열을 만들고 register 창에서 변수 b를 byte 단위로 확인해보면 

```
{0x1, 0x0, 0x1, 0x0, 0x1, 0x0, 0x1, 0x0, 0x1, 0x0}
```

위처럼 들어가있다. word 단위로 1을 넣었기 때문에 byte 단위로 쪼개서 볼 경우 0x01이 나누어져서 각각 0x1, 0x0으로 들어가는 것이다. 이에 대한 내용은 [Byte Order, 바이트 오더](https://chw-owo.github.io/os/Byte-Order,-%EB%B0%94%EC%9D%B4%ED%8A%B8-%EC%98%A4%EB%8D%94/) 포스트 참조

어셈블리어에서 변수의 값을 출력하는 것은 해당 주소값에 들어있는 값을 출력하는 것을 의미한다. 

```
PRINT_HEX 1, [a]
```

따라서 위처럼 1 byte만 출력하는 코드가 있을 경우 1,2,3,4,5가 순차로 출력되는 것이 아니라, a 변수 시작 위치에 들어있는 1 하나만 출력된다.

참고로 데이터를 읽어올 때도 수가 Little Endian이라고 가정하고 읽어오기 때문에, 넣었던 데이터 단위와 다른 단위를 사용해 출력하면 숫자가 뒤집어져서 나오게 된다. 

```
PRINT_HEX 2, [a]
```
위 코드의 경우 201을,

```
PRINT_HEX 4, [a]
```
위 코드의 경우 30201을 출력한다. 

<br/>

만약 순차로 1,2,3,4,5가 나오게끔 출력하고 싶다면 위처럼 반복문을 활용해줘야 한다.

```
CMAIN:

    xor ecx, ecx

LABEL_PRINT:
    PRINT_HEX 1, [a+ecx]
    NEWLINE
    inc ecx                 ; = add ecx, 1
    cmp ecx, 5
    jne LABEL_PRINT

    xor rax, rax
    ret

```

## **6. 함수**

어셈블리어에서는 Function(함수) 대신 Procedure, Subroutine 등의 용어를 일반적으로 사용한다. 

**1) 함수 선언**

```
CMAIN:

    call PRINT_MSG
    xor rax, rax
    ret

PRINT_MSG:
    PRINT_STRING msg
    ret

section .data
    msg db 'Hello Wolrd', 0x00

```
생긴 것 자체는 라벨과 유사하지만, 라벨과 달리 값을 반환하며 다시 이전 함수(Main 함수 포함)의 위치로 돌아간다.

지금처럼 단순 출력만 하는 함수가 아니라, 함수 내에서 복잡한 연산이 이루어지는 경우엔 어디에 값을 저장하여 반환할지도 역시 중요해진다. 함수 내에서 함수를 호출하거나, rax와 rbx에 이미 중요한 값이 있는 경우엔 함수에서 레지스터에 직접 접근하는 게 위험할 수 있다. data. bss 영역에 변수를 선언해서 인자를 할당하는 것 역시 인자가 많아질 경우 공간 낭비가 심해지기 때문에 좋지 않은 방법이다. 따라서, 함수가 유효한 동안에만 유효한, 유효 범위와 정리의 개념이 존재하는 공간이 필요하다. 그리고 메모리 영역 중 stack에 해당하는 영역이 이러한 역할을 하게 된다. 이처럼 함수에서 매개 변수를 전달하거나 돌아갈 주소를 관리할 때, 바로 stack을 사용하는 것이다. stack을 사용하여 함수를 만들기 전에 알아야 할 것이 몇 가지 있다. 

<br/>

**포인터 레지스터**

레지스터에는 범용 레지스터인 _ax, _bx, _cx, _dx 외에도 포인터 레지스터인 ip, sp, bp 등이 존재한다. 

**ip, instruction pointer**: 다음에 수행할 명령어를 가리킨다. 어셈블러에서 Registers 윈도우를 봤을 때, rip, 혹은 eip로 표시된다. 

**sp, stack pointer**: 현재 스택에서 가장 최근에 쌓인 데이터의 주소를 가리킨다. 이때, 스택은 높은 주소에서 시작해 낮은 주소 방향으로 쌓이기 때문에 스택 영역의 가장 높은 주소를 의미한다. 어셈블러에서 Registers 윈도우를 봤을 때, rsp, 혹은 esp로 표시된다. 

**bp, base pointer**: 보통 함수를 호출할 때, stack에서 해당 함수의 시작 위치를 bp에 저장하면서 시작하여 스택의 상대주소를 계산하기 위해 사용한다. 어셈블러에서 Registers 윈도우를 봤을 때, rbp, 혹은 ebp로 표시된다. 

이러한 sp, bp를 이용해 스택의 상대 주소를 계산할 수 있게 된다.

<br/>

**stack이란?**

stack에 대한 자세한 설명은 이 [포스트](https://chw-owo.github.io/os/%EC%8A%A4%ED%83%9D%EA%B3%BC-%EC%8A%A4%ED%83%9D-%ED%94%84%EB%A0%88%EC%9E%84/)를 참고하면 된다. 

stack은 후입선출 특징을 갖는 메모리의 한 영역이다. stack에 값을 저장할 때는 push 명령어를, 인출과 함께 제거할 때는 pop명령어를 활용하게 된다. 

```
push 1
```
stack에 1이라는 값을 쌓는 걸 의미한다. 

```
pop rax
```
stack의 top에 있는 값을 인출하여 rax에 넣고 stack에서 해당 값을 제거하는 것을 의미한다. 

```
CMAIN:

    push 1
    push 2 
    push 3

    pop rax
    pop rbx
    pop rcx

    xor rax, rax
    ret

```

아래와 같이 호출했을 때 register를 확인해보면 rax, rbx, rcx 순서로 3, 2, 1이 각각 들어간 것을 확인할 수 있다. 

stack에 데이터가 어떻게 쌓이고 싶은지 보고 싶다면, Registers 창에서 bp (rbp 혹은 ebp) 주소를 찾아서 Memory 창에 넣어보면 된다. 이때 stack은 높은 주소에서 낮은 주소로 쌓인다는 점에 유의하여서, 자신의 운영체제에 맞게 값을 하나씩 줄여서 입력하면 stack에 어떤 값이 쌓이고 있는지 확인할 수 있다. 예를 들어, 64bit 기준 만약 시작 주소가 0x60fe38이었다면 0x60fe30, 0x60fe28, 0x60fe20... 와 같이 적으면 stack에 쌓이는 데이터를 순서대로 볼 수 있다. 

<br/>


**stack을 사용한 함수 예시**

stack을 사용한 함수 예시는 다음과 같다.

```
CMAIN:

    push 1
    push 2
    call MAX

    xor rax, rax
    ret

MAX:
    push rbp
    mov rbp, rsp

    ...

    pop rbp
    ret

```

이것처럼 기본적으로 새로운 함수를 시작할 때, bp에 함수가 시작하는 주소값을 복사해준 뒤 사용하게 된다. 참고로 이때 push된 bp와 레지스터의 bp 값은 다르다. 그리고 해당 함수가 끝날 때, 함수의 복귀를 위해 sp를 bp로 되돌린다 뒤 pop으로 stack에 저장되어있던 bp를 다시 bp 레지스터에 저장한다. 이로써 함수 호출 이전의 bp, sp로 다시 돌아오게 된다. 이제 함수 내용을 채워보자. 


```
CMAIN:

    push rax        
    push rbx

    ; 함수에서 사용하기에 앞서, 중요한 것이 저장되어 있을 경우를 대비해 값을 복사해서 저장해두는 것. 이 예시 코드에서 다시 사용되지는 않는다. 

    push 1
    push 2

    call MAX
    PRINT_DEC 8, rax

    add rsp, 16     ; pop 두 번과 같은 의미. 1,2를 제거해준다.
    pop rax
    pop rbx

    xor rax, rax
    ret

MAX:
    push rbp
    mov rbp, rsp

    mov rax, [rbp+16]   
    mov rbx, [rbp+24]
    ; bp를 기준으로 인자의 상대 위치를 구해 계산에 사용한다

    cmp rax, rbx
    jg  LABEL_GREATER
    mov rax, rbx

LABEL_GREATER:
    pop rbp
    ret

```

위의 MAX 함수는 인자값인 1과 2를 비교하여 rax에 더 큰 값을 넣어주는 함수이다. MAX 함수가 시작한 위치를 bp에 넣은 뒤, 이를 기준 삼아 인자값을 가져와 계산하고, 함수가 종료되기 전 pop으로 되돌리는 것을 볼 수 있다. 

<br/>

## **출처**

[C++과 언리얼로 만드는 MMORPG 게임 개발 시리즈] Part1: C++ 프로그래밍 입문, Rookiss

https://m.blog.naver.com/PostView.naver?isHttpsRedirect=true&blogId=hjyang0&logNo=152390859
